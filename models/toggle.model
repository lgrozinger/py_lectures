model genetic_toggle

  // species and reactions
  var species TF1, TF2, RNA1, RNA2, I1, I2, COMPLEX1, COMPLEX2;

  transcription1: -> RNA1; v1 * K1^n1 / (K1^n1 + TF2^n1);
  transcription2: -> RNA2; v2 * K2^n2 / (K2^n2 + TF1^n2);

  rna_deg1: RNA1 -> ; d1 * RNA1;
  rna_deg2: RNA2 -> ; d2 * RNA2;

  translation1: RNA1 -> RNA1 + TF1; v3 * RNA1;
  translation2: RNA2 -> RNA2 + TF2; v4 * RNA2;

  prot_deg: TF1 -> ; d3 * TF1;
  prot_deg: TF2 -> ; d4 * TF2;

  inactivation1: I1 + TF1 -> COMPLEX1; kf1 * I1 * TF1;
  inactivation2: I2 + TF2 -> COMPLEX2; kf1 * I2 * TF2;

  reactivation1: COMPLEX1 -> I1 + TF1; kr1 * COMPLEX1;
  reactivation2: COMPLEX2 -> I2 + TF2; kr2 * COMPLEX2;

  complex_deg1: COMPLEX1 -> ; d5 * COMPLEX1;
  complex_deg2: COMPLEX2 -> ; d6 * COMPLEX2;

  // set parameters here:
  v1 = 1e0;
  v2 = 1e0;

  v3 = 1e-1;
  v4 = 1e-1;

  d1 = 1e-1;
  d2 = 1e-1;
  d3 = 1e-1;
  d4 = 1e-1;
  d5 = 1e-1;
  d6 = 1e-1;

  kf1 = 1e-1;
  kf2 = 1e-1;
  kr1 = 1e-3;
  kr2 = 1e-3;

  K1 = 0.5;
  K2 = 0.5
  n1 = 2;
  n2 = 2;

  // intial conditions (rest are zero)
  TF2 = 1e0;

  // induction events
  at (time > 120.0): I1 = 5e1;
  at (time > 360.0): I2 = 5e1;
end